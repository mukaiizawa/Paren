; n queen problem.

(import :point)
(import :matrix)

(function show (board)
  (domatrix (p board)
    (if (= (.y p) 0) (write-line))
    (if (.at board p) (write-mem "X")
        (write-mem "-")))
  (write-line))

(function putable? (board p)
  (let (x (.x p) y (.y p))
    (dotimes (y (.width board))
      (if (.at board (Point.of x y)) (return nil)))
    (for (q p) (.inside? board q) (<- q (Point.of (-- (.x q)) (-- (.y q))))
      (if (.at board q) (return nil)))
    (for (q p) (.inside? board q) (<- q (Point.of (++ (.x q)) (-- (.y q))))
      (if (.at board q) (return nil)))
    true))

(function put-queen (board y)
  (if (= y (.width board)) (show board)
      (dotimes (x (.width board))
        (let (p (Point.of x y))
          (when (putable? board p)
            (.put board p true)
            (put-queen board (++ y))
            (.put board p nil))))))

(function! main (args)
  (let (size (if (cdr args) (str->num (cadr args)) 8)
             board (.init (.new Matrix) (Point.of size size)))
    (timeit (put-queen board 0))))
