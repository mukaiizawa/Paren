; n queen problem.

(import :point)
(import :matrix)

(function show (board)
  (domatrix (p board)
    (if (= (.y p) 0) (write-line))
    (if (.at board p) (write-bytes "X")
        (write-bytes "-")))
  (write-line))

(function putable? (board p)
  (let (x (.x p) y (.y p))
    (dotimes (y (.width board))
      (if (.at board (point x y)) (return nil)))
    (for (q p) (.inside? board q) (q (point (-- (.x q)) (-- (.y q))))
      (if (.at board q) (return nil)))
    (for (q p) (.inside? board q) (q (point (++ (.x q)) (-- (.y q))))
      (if (.at board q) (return nil)))
    true))

(function put-queen (board y)
  (if (= y (.width board)) (show board)
      (dotimes (x (.width board))
        (let (p (point x y))
          (when (putable? board p)
            (.put board p true)
            (put-queen board (++ y))
            (.put board p nil))))))

(function! main (args)
  (let (size (if args (int (car args)) 8)
             board (.init (.new Matrix) (point size size)))
    (timeit (put-queen board 0))))
